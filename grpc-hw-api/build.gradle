group 'io.grpc.examples'
version '1.0.0-SNAPSHOT'
def artifactId = "grpc-hw-api"

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'maven'

ext {
    grpcVersion = '1.3.0'
    googleProtobufProtoc = '3.2.0'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'org.apache.curator:curator-test:2.9.1'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:2.1.0-beta.124'
    compile 'ch.qos.logback:logback-core:1.1.7'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'org.apache.curator:curator-framework:2.9.1'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${googleProtobufProtoc}"
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }

    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

idea {
    module {
        sourceDirs += file("src/generated/main/java")
        sourceDirs += file("src/generated/main/grpc")
        generatedSourceDirs += file("src/generated/main/java")
        generatedSourceDirs += file("src/generated/main/grpc")
    }
}

buildscript {
    ext {
        protobufGradlePluginVersion = '0.8.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVersion}"
    }
}

// gradle install安装到.m2本地仓库
install {
    repositories.mavenInstaller {
        pom.version = "$project.version"
        pom.artifactId = "$artifactId"
        pom.groupId = "$project.group"
    }
}

// gradle upload上传Jar包到maven仓库
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://maven.wz-inc.com/content/repositories/snapshots/") {
                authentication(userName: "Snapshot_Upload", password: "P@ssw0rd")
            }
            pom.version = "$project.version"
            pom.artifactId = "$artifactId"
            pom.groupId = "$project.group"
        }
    }
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
